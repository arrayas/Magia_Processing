# Orden

Cuando un programa se ejecuta el ordenador empieza leyendo la primera
línea, hace lo que pone, y así sucesivamente hasta que llega a la
última línea y se para. Así el orden de nuestras intrucciones
importa. Un buen mago debe de ser ordenado a la hora de elaborar
pociones.

Para ayudar a mantener el orden, un mago puede hacer anotaciones en un
programa para recordar qué es lo que estamos tratando de hacer. Las
anotaciones se hacen escribiendo `//`. Todo lo que aparece después en la
misma línea el ordenador lo ignora.

Veamos cómo el orden importa cuando estamos dibujando. Teclea el
siguiente ejemplo:

``` java
size(480, 120);
ellipse(140, 0, 200, 160);
// El rectángulo se dibuja sobre la elipse porque 
// aparece después en el programa
rect(160, 30, 260, 20);
```

Obtendrás la figura [1]. Si cambias el orden tecleando:

``` java
size(480, 120);
rect(160, 30, 260, 20);
// La elipse se dibuja sobre el rectángulo 
// porque aparece en el código después.
ellipse(140, 0, 200, 160);
```

verás la figura [2]. Ahora la elipse está encima del rectángulo. Es
como pintar con un pincel: lo último que pintas es los que aparce sobre
los otros objetos que has pintado antes.

[Figura 1](pictures/ps6_1.png)

[Figura 2](pictures/ps6_2.png)

Habrás observado que usamos `//` para escribir comentarios que nos
ayudan a describir lo que hace el programa. Un buen mago usa comentarios
que le ayuda después de mucho tiempo a poder leer el código de sus
pociones y a recordar lo que hacían. Te recomendamos que uses
comentarios en tus pociones.

# Propiedades del lápiz

Podemos controlar además el tamaño de los puntos y el grosor de las
líneas que deibujamos, cómo acabamos las líneas, y la forma de los
vértices de los polígonos que dibujamos.

El grosor del lápiz que usamos para dibujar se controla con la función
`strokeWeight()` (grosor o peso del trazo en inglés). El argumento de la
función es el número de píxeles que rellenamos al pintar. Intenta el
siguiente código.

``` java
size(480, 120);

strokeWeight(3); // grosor 3 píxeles

circle(75, 60, 90);
point(75,60); // punto en el centro del círculo
ellipse(225, 60, 120, 90);

strokeWeight(20); // cambio de grosor a 20 píxeles

circle(380, 60, 90);
```

Preuba y obtendrás la figura [3].

[Figura 3](pictures/ps6_3.png)

# Propiedades de las líneas

Por defecto, cuando pintamos una línea, el final es redondeado. Si el
grosor del lápiz no es grande no podemos verlo, pero si lo incrementamos
según hemos visto antes, podemos apreciarlo. Para modificar este final
del línea, podemos usar la palabra mágica `strokeCap()` ("cap" en inglés
es gorra, por lo que podemos entender que es la "gorra con la que
acabamos el trazo"). Admite un argumento que puede ser `ROUND, SQUARE` o
`PROJECT`.

``` java
  strokeCap(ROUND);
  strokeCap(SQUARE);
  strokeCap(PROJECT);
```

Por defecto es redondeado el final de línea. Pero si usamos `SQUARE`, el
final es cuadrado, sin embargo podemos usar también `PROJECT`. Lo que
hace es projectar el tamaño del círculo a un cuadrado, ocupando un poco
más. Cada vez que cambiamos el modo, todas lo que se dibuja después se
ve afectado, a no ser que lo cambiemos de nuevo.

Escribe el siguiente código y cada vez que completes una línea
ejecútalo.

``` java
size(400, 400);

line(80,20,80,380); // Dibujamos 5 líneas con trazo fino
line(320,20,320,380); 
line(10,120,390,120);
line(10,200,390,200);
line(10,280,390,280);

strokeWeight(43.0); // Cambiamos el trazo

strokeCap(ROUND);  // Esta línea no haría falta
                   // porque la opción por defecto 
                   // es final redondeado 
line(80, 120, 320, 120); // El final redondeado 
                        //  extiende la línea

strokeCap(SQUARE);  // Acabamos ahora en cuadrado 
line(80, 200, 320, 200); // Observa que la línea 
                       // empieza en x = 20 y acaba
                       // en x = 320

strokeCap(PROJECT); // Acabamos en un cuadrado extendido
                    // como en el caso ROUND
line(80, 280, 320, 280);
```

Al final, deberías ver la figura [4].

[Figura 4](pictures/ps6_4.png)

# Uniones de los lados

Cuando dibujamos cuadrados, triángulos, etc, las uniones de los lados se
pueden hacer de tres maneras. Por defecto se hacen en escuadra ("miter"
en inglés). Pero se pueden hacer en bisel, o redondeada. Tenemos pues
una nueva palabra `strokeJoin()` o unión del trazo, que admite un
argumento con tres posibles valores: `MITER`, `BEVEL` o `ROUND`.

``` java
  strokeJoin(MITER);
  strokeJoin(BEVEL);
  strokeJoin(ROUND);
```

Intenta el siguiente código.

``` java
size(480, 420);

strokeWeight(12);

strokeJoin(MITER); // Es la opción por defecto
                   // une las líneas en escuadra
square(60, 25, 70);

strokeJoin(BEVEL); // Corta las uniones en bisel
rect(60, 145, 250, 110);

strokeJoin(ROUND); // Redondea las uniones
triangle(60, 300, 60, 370, 420, 300);
```

Obtendrás la figura [5]}.

[Figura 5](pictures/ps6_5.png)

# Experimenta tus pociones

1.  Dibuja dos círculo concéntricos de 40 y 50 píxeles de diámetro
    respectivamente.

2.  Dibuja en un papel de tamaño 480x120 media elipse, que empize en 0 y
    acabe en 180 grados, y que tenga un grosor de 5 píxeles.

3.  Intenta el siguiente código:

    ``` java
        size(200,200);
        strokeWeight(30);
        strokeCap(ROUND);
        point(100,100);
    ```

    Cambia `ROUND` por `PROJECT` y ejecútalo. Luego cambia `PROJECT` por
    `SQUARE`. ?'Qué sucede en este último caso? Si no ves nada piensa
    que es como pintar un punto con grosor 0.

4.  Comenta la línea de `strokeCap` sin borrarla usando `//`. Cambia el
    grosor del lápiz a distintos valores. Si pones 0, ¿qué sucede? ¿se
    parece a algún caso del ejercicio anterior?

5.  Pinta un rectángulo de 100x50, con un grosor de trazo de 10 y las
    esquinas redondeadas.
