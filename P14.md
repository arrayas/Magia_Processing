# Columnas y filas

Teclea el siguiente código

``` {.java bgcolor="olive!10"}
size(480, 120);
background(0);
noStroke();
for (int y = 0; y < height+40; y += 40) { 
  fill(255, 140, 6);
  ellipse(0, y, 40, 40); // Pinta una columna de círculos
}
for (int x = 0; x < width+40; x += 40) {
  fill(255, 140, 6);
  ellipse(x, 0, 40, 40); // Pinta una fila de círculos
}
```

Usamos dos bucles tipo `for` para pintar primero una culumna de círculos
con coordenada horizontal del centro en `x=0`. Los centros están
espacidos vertiaclamente cada 40 píxeles ya que vamos actualizando la
coordenada `y` de 40 en 40. Por último se para cuando nos pasamos de la
altura de la ventana `height` que es de 120. Podríamos escribir de
condición del test `y <= height` que es lo mismo que usar
`y <  height+40` o incluso `y <  height+1`. La fila de círculos están a
la altura `y=0` y de nuevo espaciados 40 píxeles horizontalmente a lo
largo de la ventana que tiene una anchura `width=480`. Se obtiene la
figura [1].

[Figura 1](pictures/ps14_1.png)

# Bucles anidados

Imagina ahora que queremos rellenar como si se tratase de un suelo con
losetas toto el espacio de la ventana. Podríamos usar varios bucles de
filas, o varios bucles de columnas, pero existe un truco que todo mago
debe saber. Se trata de meter un bucle dentro de otro. Se llaman bucles
anidados o "nested loops" en inglés. Escribe el siguiente código:

``` {.java bgcolor="olive!10"}
size(480, 120);
background(0);
noStroke();
for (int y = 0; y < height+40; y += 40){//bucle exterior,columnas
  for (int x = 0; x <= width+40; x += 40){ //bucle interior,filas
    fill(255, 140, 6);
    ellipse(x, y, 40, 40); // para cada valor de y, 
                           // vamos cambiando x
  } // se cierra el bucle interior
} // se cierra el bucle exterior
```

y obtendrás la figura [2].

[Figura 2](pictures/ps14_2.png)

Cuando un bucle está dentro de otro, el número de repeticiones se
multiplica. En la columna aparecía 4 círculos, y en la fila 13, por lo
que tenemos ahora `4 * 13 = 52`$ círculos (entre cuartos, medios y enteros).

Veamos cómo funciona los bucles anidados. Empezamos con el bucle
exterior, que inicia `y = 0`. Como es menor que la altura (`height`) de
la ventana ejecuta el bloque. Dentro del bloque hay otro bucle así que
le cede el control al bucle de las filas. El bucle de las filas
empezando en `x = 0`, pinta la primera fila. Cuando acaba le devuelve el
control al bucle de las columnas. Éste cambia el valor de `y` haciendo
`y=40`, comprueba que sigue dentro de la altura de la ventana, y vuelve
a ejecutar el bucle de las filas, dibujando la siguiente fila.

Fíjate tambíen en la linea que dibuja las circunferencias.

``` {.java bgcolor="olive!10"}
 ellipse(x, y, 40, 40)
```

Aunque está dentro el bucle interior, usamos la variable `y` del bucle
externo, para situar el centro.

# Líneas chivato

Cuando un mago desarrolla pociones complejas puede cometer fallos. Se le
puede olvidar cerrar un paréntesis, poner ";" al final de una línea,
etc. Normalmente esos fallos los detecta Processing y te avisa con un
mensaje de error en la cónsola. Sin embargo existen otros fallos más
difíciles de detectar. Son tal peligrosos que han hecho que incluso se
produzcan accidentes de aviones. Estos fallos o errores se llaman
"bichitos" en inglés (bugs) porque el 9 de septiembre 1947 la gran maga 
Grace Murray Hopper, que trabajaba en la
Universidad de Harvard, encontró, junto con otros miembros de su equipo, que el ordenador Mark II
había sufrido un fallo debido a una polilla o insecto que se había quedado
pegada a un relé electromagnético (un relé es una especie de interruptor).
Grace escribió en el cuaderno de bitácula (diario) del laboratorio:
primer caso real de bichito encontrado.

Un gran poder implica una gran responsabilidad y cuando se escriben
pociones complejas es posible que empiecen a aparecer fallos difíciles de detectar, como
los mosquitos. Un ejemplo lo encontramos cuando anidamos bucles. A veces no
es fácil ver qúe es lo que está pasando si estamos usando muchas
variables. La manera más básica de corregir errores o desparasitar el
programa ("debugging" en inglés) es añadir líneas `println()` a lo largo
del programas. Este comando es como una navaja multiusos. Nos ayuda a analizar el programa y a
comprobar que está haciendo lo que debería de hacer y no otra cosa.
Cada comando `println()` funciona como un chivato y cuando no hay nada más que nos pueda ayudar
debemos utilizarlo.

En nuestro caso, podríamos añadir dos de estas palabras mágicas para ver en la cónsola cuándo el
ordenador está en un bucle y cuando está en otro, y si los valores de
las variables son los correctos. Escribe:

``` {.java bgcolor="olive!10"}
size(480, 120);
background(0);
noStroke();
for (int y = 0; y < height+40; y += 40){
  println("Bucle externo tiene el control","y =",y); // chivato
  for (int x = 0; x <= width+40; x += 40){ 
    println("Buble interno,","x =",x,"y =",y); //chivato
    fill(255, 140, 6);
    ellipse(x, y, 40, 40);                            
  } 
} 
```

Usa el ratón para ver todo lo que se muestra en la cónsola. Las líneas
chivato nos muestran qué es lo que está pasando. Te pueden salvar de un
mal hechizo.

# El poder de los bucles anidados

Vamos a ver dos ejemplos en los que vamos a usar el poder de los bucles
anidados. Intenta comprender por tu cuenta cómo fucionan esas pócimas.

El primer ejemplo es el siguiente. Teclea y ejecuta la siguiente poción:

``` {.java bgcolor="olive!10"}
size(480, 120);
background(0);
fill(255);
stroke(5,108,18);
for (int y = 20; y <= height-20; y += 10) {
  for (int x = 20; x <= width-20; x += 10) {
    ellipse(x, y, 4, 4);
    line(x, y, 240, 60); // Dibuja una línea desde el punto
                         // al centro de la ventana
  }
}
```

Obtendrás la figura [3]. Observa que hemos definido los colores para los
puntos y las líneas fuera de los bucles con `fill()` y `stroke()`.

[Figura 3](pictures/ps14_3.png)

El segundo ejemplo resulta de escribir:

``` {.java bgcolor="olive!10"}
size(480, 120);
background(0);
for (int y = 32; y <= height; y += 8) {
  for (int x = 12; x <= width; x += 15) {
    ellipse(x + y, y, 16 - y/10.0, 16 - y/10.0);
  }
}
```

Obtendrás la figura [4].

[Figura 4](pictures/ps14_4.png)

# Experimenta tus pociones

1.  Aquí tienes un nuevo diseño basado en cuadrados. Especificamos el
    centro de cada cuadrado usando el modo `rectMode(CENTER)`.

    ``` {.java bgcolor="olive!10"}
    size(480, 125);
    background(0);
    noStroke();
    rectMode(CENTER);
    fill(100);
    for (int y = 0; y <= height; y += 25) {
      for (int x = 0; x <= width; x += 25) {
        rect(x, y, 20, 20);
      }
    }
    ```
    Deberías obtener la figura [5].
    
    [Figura 5](pictures/ps14_5.png)

    

2.  Fíjate lo que sucede si metemos el comando `fill()` dentro del bucle
    interior.

    ``` {.java bgcolor="olive!10"}
    size(480, 125);
    background(0);
    noStroke();
    rectMode(CENTER); 
    for (int y = 0; y <= height; y += 25) {
      for (int x = 0; x <= width; x += 25) {
        fill(x+10,y+30,0);
        rect(x, y, 20, 20);
      }
    }
    ```
    Podemos hacer que cada cuadrado se pinte con un color diferente
    haciendo que dependa de las variables `x` e `y`, como vemos en la
    figura [6].
   
    [Figura 6](pictures/ps14_6.png)

    
